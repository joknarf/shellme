#!/usr/bin/env expect
# shellme
# Author: joknarf

set prompt_re {\n?.*[\$#] $}
set base_ms 120
set jitter_ms 120
set wait_type 2
set wait_enter 2

proc human_type {str} {
    global base_ms jitter_ms
    set len [string length $str]
    for {set i 0} {$i < $len} {incr i} {
        set ch [string index $str $i]
        set delay [expr {$base_ms + int(rand() * (2*$jitter_ms + 1)) - $jitter_ms}]
        if {$delay < 0} { set delay 0 }
        after $delay
        send_user -- $ch
        send      -- $ch
    }
}

set cmds {}
set read_from_file 0
set file_path ""

for {set i 0} {$i < $argc} {incr i} {
    set arg [lindex $argv $i]
    if {$arg eq "-f"} {
        incr i
        if {$i >= $argc} { puts stderr "ERROR: -f requires a file path"; exit 2 }
        set file_path [lindex $argv $i]
        set read_from_file 1
    } elseif {$arg eq "--prompt"} {
        incr i
        if {$i >= $argc} { puts stderr "ERROR: --prompt requires a regexp value"; exit 2 }
        set prompt_re [lindex $argv $i]
    } elseif {$arg eq "--sleep"} {
        incr i
        if {$i >= $argc} { puts stderr "ERROR: --sleep requires seconds"; exit 2 }
        set wait_type [lindex $argv $i]
        incr i
        set wait_enter [lindex $argv $i]
    } elseif {$arg eq "--speed"} {
        incr i
        if {$i+1 >= $argc} { puts stder "ERROR: --speed requires two integers: base_ms jitter_ms"; exit 2 }
        set base_ms [lindex $argv $i]
        incr i
        set jitter_ms [lindex $argv $i]
    } else {
        lappend cmds $arg
    }
}

if {$read_from_file} {
    if {![file exists $file_path]} { puts stderr "ERROR: File not found: $file_path"; exit 2 }
    set fh [open $file_path r]
    while {[gets $fh line] >= 0} {
        set tline [string trim $line]
        if {$tline eq ""} continue
        if {[regexp {^\s*#} $tline]} continue
        lappend cmds $tline
    }
    close $fh
}

if {[llength $cmds] == 0} {
    puts "Usage:"
    puts "  $argv0 \[--prompt <regexp>] \[--sleep <before_s> <after_s>] \\"
    puts "         \[--speed <base_ms> <jitter_ms>] \\"
    puts "         \[-f <file_with_commands>] | <cmd1> <cmd2> ..."
    puts "  regexp    : regexp to expect to get prompt, default '\\n?.*\[\$#] $'"
    puts "  before_s  : seconds to wait before typing, default 2"
    puts "  after_s   : seconds to wait after typing, default 2"
    puts "  base_ms   : typing char mseconds, default 120"
    puts "  jitter_ms : typing jitter mseconds, default 120"
    exit 1
}

if {[info exists env(SHELL)]} { set shell $env(SHELL) } else { set shell "/bin/bash" }
if {[info exists env(FLY_HOME)]} {
    set opts [list -c "FLY_ETC_RC=false;. $env(FLY_HOME)/.fly.d/fly loginshell"]
} else { set opts [list -i] }

# clear screen
send "\033c"
set stty_init {-echo raw}
spawn -noecho $shell {*}$opts
set timeout 600

foreach cmd $cmds {
    expect -re $prompt_re
    after [expr {$wait_type * 1000}]
    human_type $cmd
    after [expr {$wait_enter * 1000}]
    send_user -- "\n"
    send      -- "\r"
}
expect -re $prompt_re
after [expr {$wait_type * 1000}]

